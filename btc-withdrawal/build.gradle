buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //Spring-boot
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}
apply plugin: 'application'
apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
mainClassName = "withdrawal.btc.BtcWithdrawalMain"

dependencies {
    compile project(":btc")
}

// Forms a list full of 'sendBtc' task arguments
def getSendBtcArgs() {
    List<String> args = new ArrayList<>()
    if (project.hasProperty("address")) {
        args.add(project.property("address").toString())
    }
    if (project.hasProperty("amountSat")) {
        args.add(project.property("amountSat").toString())
    }
    return args
}

task runBtcWithdrawal(type: JavaExec) {
    main = 'withdrawal.btc.BtcWithdrawalMain'
    classpath = sourceSets.main.runtimeClasspath
    setWorkingDir("$rootDir/")
}

task sendBtc(type: JavaExec) {
    main = 'withdrawal.btc.send.BtcSendBtc'
    args getSendBtcArgs()
    classpath = sourceSets.main.runtimeClasspath
    setWorkingDir("$rootDir/")
}

sonarqube {
    properties {
        property "sonar.projectKey", "notary:btc-withdrawal"
    }
}
