buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}
apply plugin: 'application'
apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
mainClassName = "com.d3.btc.withdrawal.BtcWithdrawalMain"

dependencies {
    compile project(":btc")
}

// Forms a list full of 'sendBtc' task arguments
def getSendBtcArgs() {
    List<String> args = new ArrayList<>()
    if (project.hasProperty("address")) {
        args.add(project.property("address").toString())
    }
    if (project.hasProperty("amountSat")) {
        args.add(project.property("amountSat").toString())
    }
    return args
}

task runBtcWithdrawal(type: JavaExec) {
    main = 'com.d3.btc.withdrawal.BtcWithdrawalMain'
    classpath = sourceSets.main.runtimeClasspath
    setWorkingDir("$rootDir/")
}

task sendBtc(type: JavaExec) {
    main = 'com.d3.btc.withdrawal.send.BtcSendBtc'
    args getSendBtcArgs()
    classpath = sourceSets.main.runtimeClasspath
    setWorkingDir("$rootDir/")
}

sonarqube {
    properties {
        property "sonar.projectKey", "notary:btc-withdrawal"
    }
}
