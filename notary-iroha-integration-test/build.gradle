buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'com.avast.gradle.docker-compose' version '0.7.1'
}

dockerCompose {
    useComposeFiles = ['../deploy/docker-compose.dev.yml','../deploy/docker-compose.yml']
}

dependencies {
    compile project(":notary-commons")
    compile project(":notary-registration")
    // We need this dependency to test our containers. 
    // 'iroha-java:testcontainers' has this dependency inside but it's slightly old
    compile group: 'org.testcontainers', name: 'testcontainers', version: '1.11.2'
    //Iroha-java related
    compile('com.github.hyperledger.iroha-java:testcontainers:5.1.1')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.slf4j:slf4j-simple:1.7.25')
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java-util
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.7.1'

    // unit tests
    testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs = ["src/integration-test/resources"]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    setWorkingDir("$rootDir/")
    // Enable JUnit5 tests
    useJUnitPlatform {
    }

    mustRunAfter test
}
check.dependsOn integrationTest

sonarqube {
    properties {
        property "sonar.projectKey", "notary:notary-iroha-integration-test"
    }
}