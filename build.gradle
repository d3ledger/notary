buildscript {
    ext.kotlin_version = '1.3.10'
    ext.ktor_version = '1.0.0'
    ext.chain_adapter_client_version= '0b0868fe755826209fdc6de05228f19aa34a1e43'
    ext.spring_boot_version = '1.5.19.RELEASE'

    repositories {
        mavenCentral()
        jcenter()
        // gradle plugins repository
        gradlePluginPortal()
    }
    configurations.maybeCreate("pitest")
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
        pitest 'org.pitest:pitest-junit5-plugin:0.3'
    }
}

plugins {
    id "com.github.dolgopolovwork.testreport" version "1"
}

group = 'd3'
version = '1.0-SNAPSHOT'


apply plugin: 'com.google.protobuf'
apply plugin: "jacoco"
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.sonarqube'

testReport {
    testFolders = Arrays.asList(
            "notary-iroha-integration-test",
            "notary-sora-integration-test",
            "notifications-integration-test")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'maven'
    repositories {
        mavenCentral()
        // for Ktor
        jcenter()
        maven { url = 'https://dl.bintray.com/kotlin/ktor' }
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    dokka {
        outputFormat = 'html'
        outputDirectory = "build/reports/dokka"
        reportUndocumented = true
        sourceDirs = files("${project.projectDir}/src/main/kotlin")
    }
}

sourceCompatibility = 1.8

// ------------------| Test dependencies |------------------

allprojects {
    dependencies {
        // unit tests
        testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
        testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
        // https://mvnrepository.com/artifact/org.mockito/mockito-all
        testImplementation group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
        implementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0")
        // for setting env variables in tests
        testImplementation ('com.github.stefanbirkner:system-rules:1.18.0'){
            exclude group: 'junit'
        }
        // to run both junit4 and junit5 tests
        testRuntime("org.junit.vintage:junit-vintage-engine:5.2.0")
    }
}

wrapper {
    gradleVersion = 4.10
}

allprojects{
    sonarqube {
        properties {
            property "sonar.github.repository", "d3ledger/notary"
            property "sonar.projectKey", "d3:notary"
            property "sonar.java.binaries", "${project.projectDir}/build/classes"
            property "sonar.java.libraries", "${project.projectDir}/build/libs"
            property "sonar.java.test.binaries", "${project.projectDir}/build/test-results/test/binary"
            property "sonar.exclusions", "notary-iroha-integration-test/**, notifications-integration-test/**, **/integration-test/**"
        }
    }
}

allprojects {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.14.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

compileKotlin.dependsOn ':generateProto'


// -------------------------| End Iroha bindings |-------------------------

// -------------------------| PIT mutation testing |-------------------------

pitest {
    pitestVersion = "1.3.0"
    testPlugin = "junit5"
    targetClasses = ['util.*', 'notary.*', 'registration.*', 'withdrawalservice.*']
    excludedClasses = ['notary.db.*', '*Test*']
    targetTests = ['util.*', 'notary.*', 'registration.*', 'withdrawalservice.*']
    avoidCallsTo = ['kotlin.jvm.internal', 'mu']
    mutators = ['CONDITIONALS_BOUNDARY', 'NEGATE_CONDITIONALS', 'REMOVE_CONDITIONALS', 'MATH', 'INCREMENTS',
                'INVERT_NEGS', 'INLINE_CONSTS', 'VOID_METHOD_CALLS']
}

// -------------------------| JaCoCo code coverage |-------------------------

task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        html.destination "${buildDir}/reports/jacoco"
        csv.enabled false
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}
