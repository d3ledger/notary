buildscript {
    ext.kotlin_version = '1.2.41'
    ext.ktor_version = '0.9.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

group 'd3'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    // for Ktor
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    // protobuf
    compile 'com.google.protobuf:protobuf-java:3.5.0'
    compile 'io.grpc:grpc-netty:1.8.0'
    compile 'io.grpc:grpc-protobuf:1.8.0'
    compile 'io.grpc:grpc-stub:1.8.0'

    // ------------------| Src dependencies |------------------

    // kotlin related
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // rx2
    compile "io.reactivex.rxjava2:rxjava:2.0.0"
    compile "io.reactivex.rxjava2:rxkotlin:2.0.0"

    // converter rx1 to rx2
    // since eth client uses rx1, we need a converter to rx2
    compile "com.github.akarnokd:rxjava2-interop:0.12.7"

    // Iroha bindings
    def iroha_bindings_dir = Paths.get(projectDir.toString(), "iroha_bindings", getCurrentOs()).toString()
    compile fileTree(dir: iroha_bindings_dir, include: '*.jar')

    // Eth library
    compile 'org.web3j:core:3.3.1'

    // Waves library
    compile 'com.wavesplatform:wavesj:0.4'

    // DB layer
    compile 'org.jooq:jooq:3.10.6'
    compile 'org.xerial:sqlite-jdbc:3.7.2'

    // logging
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'io.github.microutils:kotlin-logging:1.4.9'

    // CLI arguments
    compile group: 'com.xenomachina', name: 'kotlin-argparser', version: '2.0.6'

    // Kyber
    // TODO @muratovv: add lib

    // Ktor - http
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"

    // JSON converter
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

    // for Result
    compile group: 'com.github.kittinunf.result', name: 'result', version: '1.4.0'

    // Configuration
    compile "com.natpryce:konfig:1.6.7.0"

    // ------------------| Test dependencies |------------------

    // unit tests
    compile "org.junit.jupiter:junit-jupiter-api:5.2.0"

    testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
    // TODO @muratovv: rework with testCompile when mocks go out from production code
    compile 'org.mockito:mockito-inline:2.18.3'
    compile('com.nhaarman:mockito-kotlin:1.5.0') {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        java {
            srcDirs += "src/main"
            // iroha bindings
            srcDirs += "build/generated/source/proto/main/java"
            srcDirs += "build/generated/source/proto/main/grpc"

            srcDirs += "src/test"
        }
        proto {
            // iroha schema
            srcDirs += "vendor/iroha/schema"
            // notary schema
            srcDirs += "src/main/kotlin/sideChain/iroha/schema"

        }

    }


}

test {
    useJUnitPlatform()
    systemProperty 'java.library.path', Paths.get(projectDir.toString(), "iroha_bindings", getCurrentOs()).toString()
    println "Runtime Iroha library path:${systemProperties}"
    environment "LD_LIBRARY_PATH", Paths.get(projectDir.toString(), "iroha_bindings", getCurrentOs()).toString()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.8.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}



task buildContracts(type: Exec) {
    description 'Build Ethereum contracts'

    String contract_path = projectDir.toString().concat("/deploy/ethereum/contract")
    String bin_path = contract_path.concat("/bin")
    file(bin_path).mkdirs()

    doLast {
        logger.info(standardOutput.toString())
        logger.error(errorOutput.toString())
    }

    commandLine 'sh', '-c', 'solc <path>/*.sol --bin --abi --optimize -o <path>/bin --overwrite'.replace("<path>", contract_path)
    commandLine 'sh', '-c', 'web3j solidity generate <path>/*.bin <path>/*.abi -o . -p com.your.organisation.namee'
}


// -------------------------| Iroha bindings |-------------------------

import org.gradle.internal.os.OperatingSystem

import java.nio.file.Paths

import static org.apache.tools.ant.taskdefs.condition.Os.*

static def getCurrentOs() {
    if (OperatingSystem.current().isMacOsX()) {
        return 'mac'
    }
    if (OperatingSystem.current().isLinux()) {
        return 'linux'
    }
}

task compileIrohaBindings(type: Exec) {
    description 'Generate Iroha binding for specific platform'

    def bindings_invocation = ['sh', 'iroha_bindings.sh', getCurrentOs()]

    commandLine bindings_invocation
}

task osInfo {
    description 'Show information about the operating system'
    doLast {
        println "System:       ${OperatingSystem.current()}"
        println "Family:       ${OS_NAME}"
        println "Version:      ${OS_VERSION}"
        println "Architecture: ${OS_ARCH}"
    }
}

compileIrohaBindings.dependsOn(osInfo)

// -------------------------| End Iroha bindings |-------------------------
