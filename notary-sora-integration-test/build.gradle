import java.nio.file.Paths

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //Spring-boot
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'org.springframework.boot'

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'

    implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    implementation project(":notary-commons")
    implementation project(":notary-iroha-integration-test")
    implementation project(":notary-registration")

    implementation 'com.github.hyperledger.iroha-java:client:6.1.0'

    implementation group: 'com.github.kittinunf.result', name: 'result', version: '1.4.0'

    implementation 'khttp:khttp:0.1.0'

    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

    // unit tests
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')

    implementation 'io.github.microutils:kotlin-logging:1.6.26'
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs = ["src/integration-test/resources"]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    setWorkingDir("$rootDir/")
    // Enable JUnit5 tests
    useJUnitPlatform {
    }

    mustRunAfter test
}
check.dependsOn integrationTest

sonarqube {
    properties {
        property "sonar.projectKey", "notary:notary-sora-integration-test"
    }
}
